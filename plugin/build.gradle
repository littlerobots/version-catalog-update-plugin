plugins {
    id "org.jetbrains.kotlin.jvm"
    id 'java-gradle-plugin'
    id "com.vanniktech.maven.publish"
    id "com.gradleup.gr8"
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'nl.littlerobots.version-catalog-update'
            implementationClass = 'nl.littlerobots.vcu.plugin.VersionCatalogUpdatePlugin'
        }
    }
}

def shadeConfiguration = configurations.create("shade")

gr8 {
    def shadowedJar = create("gr8") {
        proguardFile("rules.pro")
        configuration("shade")
    }
    // Replace the regular jar with the shadowed one in the publication
    addShadowedVariant(shadowedJar)
    removeGradleApiFromApi()
    // Make the shadowed dependencies available during compilation/tests
    configurations.named("compileOnly").configure {
        extendsFrom(shadeConfiguration)
    }
    configurations.named("testImplementation").configure {
        extendsFrom(shadeConfiguration)
    }

    configurations.named("implementation").configure {
        extendsFrom(shadeConfiguration)
    }
}

// remove all runtime dependencies from publications; which is only the catalog module
components.java.withVariantsFromConfiguration(configurations.runtimeElements) {
    skip()
}

repositories {
    gradlePluginPortal()
}

dependencies {
    shade project(":catalog")
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
    compileOnly("dev.gradleplugins:gradle-api:7.3.2")
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.github.ben-manes:gradle-versions-plugin:0.40.0'
    testImplementation(gradleTestKit())
    compileOnly 'com.github.ben-manes:gradle-versions-plugin:0.40.0'
}
