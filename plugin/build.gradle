import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id "org.jetbrains.kotlin.jvm"
    id 'java-gradle-plugin'
    id "com.vanniktech.maven.publish.base"
    id 'com.gradle.plugin-publish' version "1.2.1"
    id 'com.github.johnrengelman.shadow'
}

pluginBundle {  // Removed in Gradle 8+
    website = "https://github.com/littlerobots/version-catalog-update-plugin"
    vcsUrl = "https://github.com/littlerobots/version-catalog-update-plugin.git"

    tags = ['version', 'catalog', 'dependencies', 'versions']
}

plugins.withId("com.vanniktech.maven.publish.base") {
    group = ext.GROUP
    version = ext.VERSION_NAME
    mavenPublishing {
        publishToMavenCentral("DEFAULT")
        signAllPublications()
        pomFromGradleProperties()
    }
}

gradlePlugin {

    plugins {
        versionCatalogUpdate {
            id = 'nl.littlerobots.version-catalog-update'
            displayName = 'A plugin that updates the version catalog file.'
            description = 'Provides tasks to format and update the version catalog file with the latest dependency versions.'
            implementationClass = 'nl.littlerobots.vcu.plugin.VersionCatalogUpdatePlugin'
        }
    }
}


task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
}

shadowJar {
    dependencies {
        exclude(dependency('org.jetbrains.kotlin:.*'))
    }
    mergeServiceFiles()
    // don't relocate these
    relocate 'nl.littlerobots.vcu', 'nl.littlerobots.vcu'
    relocate 'kotlin', 'kotlin'
}

tasks.shadowJar.archiveClassifier.set("")
tasks.shadowJar.dependsOn tasks.relocateShadowJar
configurations.api.dependencies.remove dependencies.gradleApi()

repositories {
    gradlePluginPortal()
}

// remove all runtime dependencies from publications; which is only the catalog module
components.java.withVariantsFromConfiguration(configurations.runtimeElements) {
    skip()
}

dependencies {
    implementation project(path: ":catalog")
    //noinspection GradlePackageUpdate
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31"
    shadow gradleApi()
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.github.ben-manes:gradle-versions-plugin:0.40.0'
    testImplementation(gradleTestKit())
}
